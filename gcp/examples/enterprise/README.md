# Example: Enterprise Materialize Deployment on GCP with Ory (OIDC/SAML)

This example extends the [simple deployment](../simple/) with **Ory Kratos** (identity management) and **Ory Hydra** (OAuth2/OIDC provider) for enterprise authentication via OIDC and SAML. Optionally adds **Ory Polis** (SAML-to-OIDC bridge with SCIM support) when `enable_polis = true`.

Ory enterprise images are pulled through the Materialize-hosted registry proxy at `ory.registry.cloud.materialize.com`. The proxy authenticates with the Materialize license key JWT, so no separate Ory credential is needed. Cluster nodes need egress to the proxy host and to `storage.googleapis.com` (the proxy returns 307 redirects to signed GCS URLs for blob GETs, which the kubelet follows directly).

---

## What Gets Created

Everything from the [simple example](../simple/README.md), plus:

### Ory Database
- **Cloud SQL for PostgreSQL** (separate instance from Materialize): Version 18
- **Tier**: db-f1-micro (suitable for Ory workloads)
- **Databases**: `kratos` and `hydra` on the same instance (plus `polis` when `enable_polis = true`)
- **Network Access**: Private IP only, same VPC as the Materialize database

### Ory Kratos (Identity Management)
- **Helm release**: Deployed in the `ory` namespace
- **Replicas**: 2 (with PodDisruptionBudget)
- **Resources**: 250m CPU request / 256Mi memory (request & limit)
- **Purpose**: Manages user identities, login/registration flows, supports OIDC and SAML providers

### Ory Hydra (OAuth2 & OIDC Provider)
- **Helm release**: Deployed in the `ory` namespace (shared with Kratos)
- **Replicas**: 2 (with PodDisruptionBudget)
- **Resources**: 250m CPU request / 256Mi memory (request & limit)
- **Maester**: Enabled (CRD controller for managing OAuth2 clients via Kubernetes resources)
- **Purpose**: Issues OAuth2 tokens, provides OIDC discovery endpoint, delegates login/consent to Kratos

### Ory Polis (optional, SAML-to-OIDC bridge)
- **Helm release**: Deployed in the `ory` namespace when `enable_polis = true`
- **Purpose**: Accepts a customer's SAML IdP on one side and exposes an OIDC provider on the other so Kratos can consume it as an upstream social sign-in. Also exposes a SCIM endpoint for IdP-driven user provisioning.
- **Chart and image**: Both pulled through the Materialize OEL registry proxy with the license-key JWT, no separate credential required
- Off by default. Set `enable_polis = true` and `ory_polis_fqdn` to deploy.

---

## Getting Started

### Step 1: Set Required Variables

Create a `terraform.tfvars` file:

```hcl
project_id  = "my-gcp-project-id"
region      = "us-central1"
name_prefix = "mz-enterprise"

# Materialize license key JWT. Used both by Materialize and as the password
# in the imagePullSecret authenticating to the Ory registry proxy. Reach out
# to Materialize sales / support to get one issued with the `ory` entitlement.
license_key = "your-materialize-license-key"

# Restrict access to the GKE master endpoint. Required (no default). For lab
# use you may pass a single 0.0.0.0/0 entry, but production should pin a
# tight allowlist.
k8s_apiserver_authorized_networks = [
  {
    cidr_block   = "203.0.113.0/24"
    display_name = "corp-vpn"
  },
]

# Public hostnames used for browser traffic and OIDC redirects. These must
# resolve to the LB IPs after apply (see "DNS records" below).
ory_hydra_fqdn             = "hydra.mz.example.com"
ory_ui_fqdn                = "auth.mz.example.com"
ory_kratos_fqdn            = "kratos.mz.example.com"
materialize_console_fqdn   = "console.mz.example.com"
materialize_balancerd_fqdn = "balancerd.mz.example.com"

labels = {
  environment = "demo"
  project     = "materialize-enterprise"
}
```

**Required Variables:**
- `project_id`: GCP project ID
- `name_prefix`: Prefix for all resource names
- `labels`: Map of labels to apply to resources
- `license_key`: Materialize license key JWT. Used for Materialize itself and as the password authenticating to the Ory registry proxy. Must carry the `ory` entitlement.
- `k8s_apiserver_authorized_networks`: List of CIDR blocks (with display names) allowed to reach the GKE master endpoint. No default; pass a single 0.0.0.0/0 entry for lab use, or a tight allowlist for production.
- `ory_hydra_fqdn`, `ory_ui_fqdn`, `ory_kratos_fqdn`, `materialize_console_fqdn`, `materialize_balancerd_fqdn`: Public hostnames for the five browser-facing endpoints (Hydra OAuth2, Kratos public API, selfservice UI, Materialize console, and balancerd, which serves the SQL-over-HTTP endpoint the console JS calls from the browser)

**Optional Variables:**
- `region`: GCP region (defaults to `us-central1`)
- `ingress_cidr_blocks`: List of CIDR blocks allowed to reach the LoadBalancer frontends (no effect when `internal_load_balancer = true`)
- `internal_load_balancer`: Whether to use internal LBs (defaults to `true`). Set to `false` for prod-like demos validated against real DNS.
- `enable_observability`: Enable Prometheus and Grafana monitoring stack (defaults to `true`)
- `enable_polis`: Deploy Ory Polis alongside Kratos and Hydra (defaults to `false`). When `true`, also set `ory_polis_fqdn` and create the corresponding DNS record. The `polis-oel` image is currently amd64-only; on arm64 clusters add a small amd64 node pool and pin Polis there via `polis_helm_values`.
- `ory_polis_fqdn`: Public hostname for Polis. Required when `enable_polis = true`.
- `polis_helm_values`: Additional Helm values for the Polis chart. Most common use is `{ deployment = { nodeSelector = { workload = "polis-amd64" } }, job = { nodeSelector = { workload = "polis-amd64" } } }` to pin Polis to a dedicated amd64 node pool.
- TLS certificate options (`cert_issuer_ref`, …): see [TLS Certificates](#tls-certificates) below.

### Step 2: Deploy

```bash
terraform init
terraform apply
```

### Step 3: Verify Ory Deployment

```bash
# Check Ory pods
kubectl get pods -n ory

# Check Kratos health
kubectl port-forward svc/kratos-admin 4434:4434 -n ory
curl http://localhost:4434/health/ready

# Check Hydra health
kubectl port-forward svc/hydra-admin 4445:4445 -n ory
curl http://localhost:4445/health/ready
```

---

## TLS Certificates

cert-manager provisions every TLS certificate in this stack. The example uses two issuers:

- An always-created in-cluster **self-signed** `ClusterIssuer` for the internal mTLS cert (which has `*.cluster.local` SANs that public ACME issuers like Let's Encrypt cannot sign).
- A configurable issuer for the browser-facing certs (Materialize console / balancerd, Hydra, Kratos, selfservice UI). Defaults to the same self-signed issuer; override via `var.cert_issuer_ref` to plug in a real one (corporate CA, Let's Encrypt, etc.).

### Default: self-signed for everything (demo / air-gapped)

No tfvars required. **Browsers will not trust the cert** and users will see warnings.

This mode requires environmentd to trust the cluster CA when fetching JWKS from Hydra. Patch the Materialize CR (or set this through the `materialize-instance` module):

```yaml
spec:
  environmentdExtraEnv:
    - name: SSL_CERT_FILE
      value: /etc/materialized/ca.crt
```

Use this mode for offline demos or air-gapped clusters where no real domain or DNS provider is available.

### Bring your own browser-facing issuer

Set:

```hcl
cert_issuer_ref = {
  name = "your-existing-cluster-issuer"
  kind = "ClusterIssuer"  # or "Issuer"
}
```

The browser-facing certs use this issuer; the internal mTLS cert continues to use the self-signed cluster issuer. Customers typically bring a corporate CA, an existing trust-manager bundle, or an ACME issuer (Let's Encrypt) backed by their preferred DNS-01 / HTTP-01 solver.

#### Example: Let's Encrypt with Cloudflare DNS-01

If you don't already have a `ClusterIssuer` in the cluster, here's a copyable starting point. Drop into your root module, customize, then point `cert_issuer_ref` at it. The same pattern works with any cert-manager solver: swap the `solvers` block for Route53, Azure DNS, Cloud DNS, HTTP-01, etc.

```hcl
# Cloudflare API token with Zone:Read + DNS:Edit on the zones you'll request
# certs for. Create at https://dash.cloudflare.com/profile/api-tokens.
variable "cloudflare_api_token" {
  type      = string
  sensitive = true
}

resource "kubernetes_secret_v1" "cloudflare_api_token" {
  metadata {
    name      = "cloudflare-api-token"
    namespace = "cert-manager"
  }
  data = {
    "api-token" = var.cloudflare_api_token
  }
}

resource "kubectl_manifest" "letsencrypt_prod" {
  yaml_body = yamlencode({
    apiVersion = "cert-manager.io/v1"
    kind       = "ClusterIssuer"
    metadata = {
      name = "letsencrypt-prod"
    }
    spec = {
      acme = {
        # While iterating, point at the staging server to avoid the production
        # rate limit (50 certs / week / registered domain). Staging certs are
        # not browser-trusted; switch back once the integration is stable.
        # server = "https://acme-staging-v02.api.letsencrypt.org/directory"
        server = "https://acme-v02.api.letsencrypt.org/directory"
        email  = "you@example.com"
        privateKeySecretRef = {
          name = "letsencrypt-prod-account-key"
        }
        solvers = [{
          selector = {
            dnsZones = ["example.com"]
          }
          dns01 = {
            cloudflare = {
              apiTokenSecretRef = {
                name = kubernetes_secret_v1.cloudflare_api_token.metadata[0].name
                key  = "api-token"
              }
            }
          }
        }]
      }
    }
  })

  depends_on = [kubernetes_secret_v1.cloudflare_api_token]
}

module "materialize_enterprise" {
  source = "..."

  cert_issuer_ref = {
    name = "letsencrypt-prod"
    kind = "ClusterIssuer"
  }

  # ...rest of the example variables
}
```

After `terraform apply`, create A records for your hostnames (Hydra, Kratos, selfservice UI, Materialize console, and Polis when `enable_polis = true`) pointing at the LB IPs. cert-manager issues certs once DNS resolves (typically 1 to 2 minutes for the first issuance).

---

## Architecture

```
┌─────────────────────────────────────────────────────────┐
│                      GKE Cluster                        │
│                                                         │
│  ┌─────────────────────┐  ┌──────────────────────────┐  │
│  │   Generic Nodes     │  │  Materialize Nodes       │  │
│  │                     │  │  (tainted)               │  │
│  │  ┌───────────────┐  │  │  ┌────────────────────┐  │  │
│  │  │  Ory Kratos   │  │  │  │  Materialize       │  │  │
│  │  │  (identity)   │  │  │  │  Instance           │  │  │
│  │  ├───────────────┤  │  │  └────────────────────┘  │  │
│  │  │  Ory Hydra    │  │  │                          │  │
│  │  │  (OAuth2)     │  │  │                          │  │
│  │  ├───────────────┤  │  │                          │  │
│  │  │  Operator     │  │  │                          │  │
│  │  │  cert-manager │  │  │                          │  │
│  │  └───────────────┘  │  │                          │  │
│  └─────────────────────┘  └──────────────────────────┘  │
└─────────────────────────────────────────────────────────┘
         │                           │
    ┌────┴────┐                ┌─────┴─────┐
    │ Ory DB  │                │   MZ DB   │
    │(f1-micro│                │(custom-2) │
    │ kratos  │                │materialize│
    │ hydra   │                │           │
    └─────────┘                └───────────┘
```

---

## Notes

- Ory Kratos and Hydra share a namespace (`ory`) but use separate databases on the same Cloud SQL instance
- The Ory Cloud SQL instance uses a smaller tier (`db-f1-micro`) since Ory workloads are lightweight
- Both Ory components are scheduled on generic nodes (not the Materialize-dedicated node pool)
- Cloud SQL on GCP has PostgreSQL extensions (pg_trgm, btree_gin, uuid-ossp) available by default, no allowlisting needed
- For production, override Kratos and Hydra chart values via `kratos_helm_values` and `hydra_helm_values` on the `module.ory` call (they deep-merge on top of the baked-in defaults), and register additional OAuth2 clients via Hydra Maester CRDs (Maester is enabled by default)
- When `enable_polis = true`, the Polis Helm chart and image are pulled through the Materialize OEL registry proxy using the same license-key JWT; no additional credential is required. The `polis-oel` image is currently amd64-only; on arm64 clusters provision a small amd64 node pool and pin Polis there via `polis_helm_values`
- Don't forget to destroy resources when finished:

```bash
terraform destroy
```
